// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name = POM_LICENSE_NAME
            url = POM_LICENSE_URL
            distribution = POM_LICENSE_DISTRIBUTION
        }
    }
    developers {
        developer {
            id = "davidgarcia"
            name = "David GarcÃ­a"
            email = "david.garcia@inqbarna.com"
        }
    }

    scm {
        url = POM_PROJECT_URL
    }
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.source
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: dokka) {
    from "$buildDir/dokka"
    archiveClassifier = 'javadoc'
}


def printObj = { o ->
    def filtered = ['class', 'active']

    println o.properties
            .sort { it.key }
            .collect { it }
            .findAll { !filtered.contains(it.key) }
            .join('\n')
}

if (project.hasProperty('bintrayUser')) {

    apply plugin: "com.jfrog.bintray"
    apply plugin: 'maven-publish'

    def pubName = "${libraryArtifactId}LibraryPublication"

    afterEvaluate {


        publishing {
            publications {
                "$pubName"(MavenPublication) {
                    from components.release
                    groupId = LIBRARY_GROUP_ID
                    artifactId = libraryArtifactId
                    version = libraryVersion
                    artifact androidSourcesJar
                    artifact javadocJar

                    pom(pomConfig)
                }
            }
        }

        bintray {
            user = project.property('bintrayUser')
            key = project.property('bintrayKey')
            publications = [pubName]
            dryRun = false
            override = false
            pkg {
                repo = BINTRAY_REPO
                name = libraryArtifactId
                userOrg = BINTRAY_ORGANIZATION
                licenses = [POM_LICENSE]
                vcsUrl = POM_REPO_URL
                version {
                    name = libraryVersion
                }
            }
        }
    }

}
